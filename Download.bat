@echo off

rem ===============================================================================================================================
rem 【Youtubeダウンローダー】
rem 引数に指定したYoutubeのURLから最高画質、最高音質で動画をダウンロードする。
rem ダウンロード先は設定ファイル（.ini）に記述する。
rem (2020/04/25 追記)ニコニコ動画のダウンロードにも対応した。画質はベストエフォート。
rem 
rem 
rem ◆使用ツール
rem ・youtube-dl	:	Youtubeから動画をダウンロードする。ちなみに他の動画サイトからもダウンロードできるみたいだが未検証。
rem ・ffmpeg		:	ダウンロードした動画のフォーマットを行う。
rem 
rem 
rem ◆パラメータ説明
rem ・%1（必須）	: ダウンロード元のYoutubeのURL
rem ・%2（任意）	: ダウンロード先のディレクトリ
rem 
rem 
rem ◆設定ファイル説明
rem ・DL_DIR_PATH			：	ダウンロード先のフォルダパス
rem ・CONSOLE_INPUT_FLG		：	引数が指定されていない場合にその引数をコンソール入力させるかどうか。
rem ・PAUSE_FLAG			：	処理の最後にpauseを実行するかどうか。親バッチから呼ばれることがあればFalseにする。
rem 
rem ===============================================================================================================================

echo.
echo ☆☆処理開始☆☆
echo.

rem ----------------------------------------------------------------------------------------
rem カレントディレクトリ移動
rem ----------------------------------------------------------------------------------------
cd /d %~dp0

rem -------------------------------------------------------------------------------------------------------------------------------
rem 初期値設定
rem -------------------------------------------------------------------------------------------------------------------------------
set DOWNLOADER_PATH=bin\youtube-dl.exe
set GET_INI_BATCH_PATH=..\Utils\Batch\GetIni.bat
set INI_FILE_PATH=config.ini

rem -------------------------------------------------------------------------------------------------------------------------------
rem INIファイル設定取得
rem -------------------------------------------------------------------------------------------------------------------------------
call %GET_INI_BATCH_PATH% :READ_INI_VAL "CONFIG" "DL_DIR_PATH" DL_DIR_PATH %INI_FILE_PATH%
call %GET_INI_BATCH_PATH% :READ_INI_VAL "CONFIG" "CONSOLE_INPUT_FLG" CONSOLE_INPUT_FLG %INI_FILE_PATH%
call %GET_INI_BATCH_PATH% :READ_INI_VAL "CONFIG" "PAUSE_FLAG" PAUSE_FLAG %INI_FILE_PATH%

rem ----------------------------------------------------------------------------------------
rem パラメータの取得
rem ----------------------------------------------------------------------------------------

rem 引数1が存在していればダウンロード元URLとして取得。なれけば終了 or コンソールから入力させる。
if not "%~1"=="" (
	set URL="%~1"
) else (
	if %CONSOLE_INPUT_FLG%==True (
		echo ダウンロード元のURLを指定してください。
		set /p URL=">>> "
		echo.
	) else (
		echo 実行引数にダウンロード元のURLを指定してください。
		echo.
		exit /b
	)
)
rem URLの前後に「"」をつける。
set URL=%URL:"=%
rem set URL="%URL%"

rem 引数2が存在していればダウンロード先ディレクトリとして取得。
if not "%~2"=="" (
	set DL_DIR_PATH=%~2
)

rem ----------------------------------------------------------------------------------------
rem ダウンロードの実行
rem ----------------------------------------------------------------------------------------
echo 以下にダウンロードします。
echo %DL_DIR_PATH%
echo.

rem URLにニコニコ動画のアドレスが含まれるかを判定。結果によってDLコマンドを分岐する。
echo "%URL%" | find "www.nicovideo.jp" > NUL
if %ERRORLEVEL%==0 goto :niconico

rem デフォルト（youtube）のダウンロードは以下のコマンドで実施。
:youtube
	bin\youtube-dl "%URL%" -f bestvideo[ext=mp4]+bestaudio[ext=m4a] --merge-output-format mp4 -o "%DL_DIR_PATH%\%%(title)s.%%(ext)s"
	goto :exit

rem ニコニコ動画の場合は以下のコマンドで実施。
rem DL途中で中断されやすいので繰り返し実行するようにする。
:niconico
	bin\youtube-dl "%URL%" -f best -o "%DL_DIR_PATH%\%%(title)s.%%(ext)s"
	if not %ERRORLEVEL%==0 (
		echo.
		echo Retrying...
		echo.
		goto :niconico
	)
	goto :exit

rem ----------------------------------------------------------------------------------------
rem 終了処理
rem ----------------------------------------------------------------------------------------
:exit
echo.
echo ☆☆処理終了☆☆
if %PAUSE_FLAG%==True pause
exit /b
